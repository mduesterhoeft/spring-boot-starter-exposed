buildscript {
	ext {
		kotlinVersion = '1.2.31'
		springBootVersion = '2.0.1.RELEASE'
	}
	repositories {
		mavenCentral()
		maven { url "https://repo.spring.io/snapshot" }
		maven { url "https://repo.spring.io/milestone" }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
		classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
	}
}

apply plugin: 'maven'
apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'jacoco'

group = 'com.md'
sourceCompatibility = 1.8

compileKotlin {
	kotlinOptions {
		freeCompilerArgs = ["-Xjsr305=strict"]
		jvmTarget = "1.8"
	}
}
compileTestKotlin {
	kotlinOptions {
		freeCompilerArgs = ["-Xjsr305=strict"]
		jvmTarget = "1.8"
	}
}

repositories {
	mavenCentral()
    jcenter()
	maven { url "https://repo.spring.io/snapshot" }
	maven { url "https://repo.spring.io/milestone" }
	maven { url  "https://dl.bintray.com/kotlin/exposed" }
}

ext['exposed.version'] = '0.10.2'

dependencies {
    compile("org.springframework.boot:spring-boot-starter:${project.ext.springBootVersion}")
    compile("org.springframework.boot:spring-boot-starter-jdbc:${project.ext.springBootVersion}")
    compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
    compile("org.jetbrains.kotlin:kotlin-reflect")
    compile("org.jetbrains.exposed:exposed:${project.ext['exposed.version']}")
    compileOnly("org.jetbrains.exposed:spring-transaction:${project.ext['exposed.version']}")

    testCompile("org.springframework.boot:spring-boot-starter-test:${project.ext.springBootVersion}")
    testCompile("org.jetbrains.exposed:spring-transaction:${project.ext['exposed.version']}")
    testCompile('org.amshove.kluent:kluent:1.35')
    testRuntime('com.h2database:h2:1.4.197')
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives sourcesJar
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

